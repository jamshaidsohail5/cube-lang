use cube.lang.cst
use cube.lang.tokenizer

define parser[T]
  function create(
    definition as parserDefinition[T], translator as cstTranslator[T], tokens as tokenStream)
    me.leftParsers = definition.left
    me.rightParsers = definition.right
    me.translator = translator
    me.tokens = tokens
  end

  function translator as cstTranslator[T] = translator
  function tokens as tokenStream = tokens
  function parse as T = parse(0)

  function parse(rightPrecedence as int) as T
    var t0 as token = tokens.next
    var left as T = leftParsers.find(t0).parse(me, t0)

    while rightPrecedence < leftPrecedence
      t0 = tokens.next
      left = rightParsers.find(t0).parse(me, left, t0)
    end

    output left
  end

  internal
    var leftParsers as parserMap[leftParser[T]]
    var rightParsers as parserMap[rightParser[T]]
    var translator as cstTranslator[T]
    var tokens as tokenStream

    function leftPrecedence as int
      var q0 as token = tokens.peek(0)
      if q0 is null then output 0
      var parser as rightParser[T] = rightParsers.get(q0)
      output if parser is not null then parser.precedence else 0
    end
  end
end