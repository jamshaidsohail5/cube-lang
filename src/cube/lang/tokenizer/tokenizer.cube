use cube.lang
use cube.lang.cst
use cube.lang.cst.cstNodeType
use cube.lang.tokenizer.characterType
use cube.lang.tokenizer.symbol

define tokenizer
  function create(text as string) = me.text = text
  function tokenType as cstNodeType = tokenType
  function tokenText as string = tokenText
  function symbol as symbol = symbol
  function keyword as keyword = keyword

  function next as boolean
    var c as int

    while c <- peek != -1 ->
      match characterType(c)
        when WHITESPACE then position++
        when SYMBOL_CHARACTER then output readSymbol
        when DIGIT then output readNumber
        when CHARACTER then output identifierOrKeyword
        else error new unsupportedOperationException
      end

    output false
  end

  internal
    var text as string
    var position as int
    var tokenType as cstNodeType
    var tokenText as string
    var symbol as symbol
    var keyword as keyword

    function readNumber
      var start as int = position++
      var c as int
      while c <- peek != -1 and characterType(c) = DIGIT -> position++
      tokenText = text.substring(start, position)
      tokenType = NUMBER
      output true
    end

    function identifierOrKeyword
      var start as int = position++
      var c as int
      while c <- peek != -1 and characterType(c) = CHARACTER -> position++
      tokenText = text.substring(start, position)
      keyword = Keyword.keyword(tokenText)

      tokenType
        = if keyword is not null then keyword.nodeType else IDENTIFIER

      output true
    end

    function readSymbol
      var c as int = read

      match c
        when char('.') then reduce(DOT)
        when char('(') then reduce(LEFT_PARENTHESIS)
        when char(')') then reduce(RIGHT_PARENTHESIS)
        when char(',') then reduce(COMMA)
        when char('=') then reduce(EQUAL)

        when char('+') then if peek = char('+') then do
          read
          reduce(PLUS_PLUS)
        end else reduce(PLUS)

        when char('-') then
          match peek
            when char('-') then do
              read
              reduce(MINUS_MINUS)
            end

            when char('>') then do
              read
              reduce(RIGHT_ARROW)
            end

            else reduce(MINUS)
          end

        when char('*') then reduce(ASTERISK)
        when char('/') then reduce(FORWARD_SLASH)
        when char('^') then reduce(CARET)
        when char('~') then reduce(TILDE)

        when char('<') then
          match peek
            when char('-') then do
              read
              reduce(LEFT_ARROW)
            end

            when char('=') then do
              read
              reduce(LESS_EQUAL)
            end

            else reduce(LESS)
          end

        when char('>') then
          if peek = char('=') then do
            read
            reduce(GREATER_EQUAL)
          end else reduce(GREATER)

        else error new unsupportedOperationException(
          "Unrecognized character '" + c as char + "' unicode " + c + '.')
      end

      output true
    end

    function reduce(symbol as symbol)
      me.tokenType = SYMBOL
      me.tokenText = symbol.text
      me.symbol = symbol
    end

    function peek as int = if position < text.length then text.charAt(position) else -1
    function read as int = text.charAt(position++)
  end
end